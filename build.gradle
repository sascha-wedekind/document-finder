import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

import java.nio.file.Paths
import java.util.regex.Pattern

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
    id 'me.qoomon.git-versioning' version '6.4.2'
    id 'org.gradlex.extra-java-module-info' version '1.8'
}

ext {
    lombokVersion = '1.18.34'
    slf4jVersion = '2.0.16'
    jclOverSlf4jVersion = '2.0.14'
    logbackVersion = '1.5.6'
    janinoVersion = "3.1.12"
    luceneVersion = '9.11.1'
    mockitoVersion = '5.4.0'
    junitVersion = '5.11.0'
    assertjVersion = '3.26.3'
    reactorVersion = '3.6.10'
    guavaVersion = '33.3.0-jre'
    tikaVersion = '2.9.2'
    daggerVersion = '2.52'
    javaFxVersion = '22.0.1'
    fontAwesomeFxVersion = '2.2.0-9.1.2'
    testFxVersion = '4.0.18'
    gsonVersion = '2.11.0'
    jsonAssertVersion = '1.5.1'
    jSystemThemeDetectorVersion = '3.9.1'
    failsafeVersion = '3.3.2'
    commonsIoVersion = '2.15.1'
    g00fy2VersionCompareVersion = '1.5.0'
    jnaVersion = '5.15.0'
    defaultMainClass = 'com.bytedompteur.documentfinder.DocumentFinderMain'
}

group 'com.bytedompteur'
version '1.0-SNAPSHOT'
OperatingSystem operatingSystem = DefaultNativePlatform.currentOperatingSystem
gitVersioning.apply {
    refs {
        considerTagsOnBranches = true
        tag('v(?<version>.*)') {
            version = '${ref.version}'
        }
        branch('.+') {
            version = '1.0-${ref}-SNAPSHOT'
        }

    }

    // optional fallback configuration in case of no matching ref configuration
    rev {
        version = '${commit}'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    all*.exclude group: 'xml-apis'
    // Exclude cryptomator linux integration dependencies, which lead to duplicate module error while executing jpackage
    all*.exclude group: 'org.purejava' // org.purejava:kdewallet
    all*.exclude group: 'de.swiesend' // de.swiesend:secret-service
    all*.exclude group: 'commons-logging', module: 'commons-logging' // replaced by jcl-over-slf4j
}

dependencies {
    //  ============================================
    //  Runtime
    //  ============================================
    implementation "dev.failsafe:failsafe:${failsafeVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "org.slf4j:jcl-over-slf4j:${jclOverSlf4jVersion}" // Route Apache Tika logs to Logback
    implementation "org.codehaus.janino:janino:${janinoVersion}"
    implementation "org.apache.lucene:lucene-core:${luceneVersion}"
    implementation "org.apache.lucene:lucene-queryparser:${luceneVersion}"
    implementation "org.apache.lucene:lucene-analysis-common:${luceneVersion}"
    implementation "io.projectreactor:reactor-core:${reactorVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.apache.tika:tika-core:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-pdf-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-miscoffice-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-microsoft-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-apple-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-text-module:${tikaVersion}"
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "de.jensd:fontawesomefx-materialicons:${fontAwesomeFxVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation ("com.github.Dansoftowner:jSystemThemeDetector:${jSystemThemeDetectorVersion}") {
        exclude group: 'net.java.dev.jna', module: 'jna-platform-jpms'
        exclude group: 'net.java.dev.jna', module: 'jna-jpms'
    }
    implementation "io.github.g00fy2:versioncompare:${g00fy2VersionCompareVersion}"
    implementation "commons-io:commons-io:${commonsIoVersion}"
    implementation "net.java.dev.jna:jna-platform-jpms:${jnaVersion}"

    //  ===================================================================================
    //  Operating system dependent runtime
    //  Because the application release build must be done on the same OS as the target OS
    //  ===================================================================================
    if (operatingSystem.isMacOsX()) {
        implementation "org.cryptomator:integrations-mac:1.2.3"
    } else if (operatingSystem.isWindows()) {
        implementation "org.cryptomator:integrations-win:1.2.5"
    } else if (operatingSystem.isLinux()) {
        implementation "org.cryptomator:integrations-linux:1.4.5"
    }

    //  ============================================
    //  Compile only
    //  ============================================
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor ("com.google.dagger:dagger-compiler:${daggerVersion}") {
        exclude group: 'org.jetbrains', module: 'annotations'
    }

    //  ============================================
    //  Test
    //  ============================================
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "com.google.dagger:dagger:${daggerVersion}"
    testAnnotationProcessor ("com.google.dagger:dagger-compiler:${daggerVersion}") {
        exclude group: 'org.jetbrains', module: 'annotations'
    }

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation 'org.hamcrest:hamcrest:3.0' // Required for FXTest. The FXRobot has a direct dependency to hamcrest.

    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "io.projectreactor:reactor-test:${reactorVersion}"
    testImplementation "org.testfx:testfx-core:${testFxVersion}"
    testImplementation "org.testfx:testfx-junit5:${testFxVersion}"
    testImplementation "org.skyscreamer:jsonassert:${jsonAssertVersion}"
}

test {
    useJUnitPlatform()
    configurations {
        testRuntimeClasspath {
            attributes { attribute(Attribute.of("javaModule", Boolean), false) }
        }
    }
}

compileTestJava {
    dependsOn('createVersionInfoPropertiesFile')
    configurations {
        testCompileClasspath {
            attributes { attribute(Attribute.of("javaModule", Boolean), false) }
        }
    }
}

jar {
    dependsOn('createVersionInfoPropertiesFile')
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
        // Subdirectory in the project which acts as the application home dir while developing.
        excludeDirs += file("index-dev")
    }
}

//As documented here https://openjfx.io/openjfx-docs/#gradle
javafx {
    version = "${javaFxVersion}"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.web' ]
}

application {
    mainClass = project.hasProperty('applicationMainClassOverwrite') ? project.getProperty('applicationMainClassOverwrite') : project.defaultMainClass
    mainModule = 'DocumentFinder.main'
}

// https://github.com/gradlex-org/extra-java-module-info
extraJavaModuleInfo {
    failOnMissingModuleInfo.set(false)

    automaticModule("javax.inject:javax.inject", "javax.inject")
    automaticModule("io.github.g00fy2:versioncompare", "versioncompare")
}

jlink {
    def osSpecificCryptomatorIntegrationsPackageRequired = null
    def semverPattern = Pattern.compile("\\d.*\\.\\d.*\\.\\d.*")
    def isSnapshotBuild = !semverPattern.matcher("${version}").matches()
    options = ['--strip-debug', '--compress', 'zip-9', '--no-header-files', '--no-man-pages', '--verbose', '--ignore-signing-information']  // Ignoring signing information is required since usage of Java 21 an Apache Tika parser (org.bouncycastle:bcpkix-jdk18on) but the signature-related files of the signed modular JARs aren't copied to the runtime image.
    launcher {
        name = isSnapshotBuild ? "DocumentFinder-SNAPSHOT-${project.property("git.commit")}" : "DocumentFinder"
    }
    jpackage {
        if (isSnapshotBuild)
            appVersion = '1.0.0'
        else
            appVersion = "${version}".replaceAll('v', '')


        vendor = 'DocumentFinder'

        if (operatingSystem.isWindows()) {
            installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-menu-group', 'DocumentFinder', '--win-upgrade-uuid', '420e9eb8-5b23-4589-88bb-94c1b0b70b8b', '--win-shortcut', '--about-url', 'https://github.com/sascha-wedekind/document-finder']
            icon = 'src/main/resources/images/DocumentFinderIcon.ico'
            installerType = 'msi'
            osSpecificCryptomatorIntegrationsPackageRequired = 'org.cryptomator.integrations.win'
        } else if (operatingSystem.isLinux()) {
            icon = 'src/main/resources/images/DocumentFinderIcon_512.png'
            installerType = 'deb'
            installerOptions = ['--about-url', 'https://github.com/sascha-wedekind/document-finder']
        } else if (operatingSystem.isMacOsX()) {
            icon = 'src/main/resources/images/DocumentFinderIcon_512.icns'
            installerType = 'dmg'
            jvmArgs = ['-Dapple.awt.enableTemplateImages=true', '-Dapple.awt.UIElement=true', '-Dapple.laf.useScreenMenuBar=true', '-Dsun.java2d.metal=true']
            installerOptions = ['--about-url', 'https://github.com/sascha-wedekind/document-finder', '--mac-package-name', 'DocumentFinder', '--mac-package-identifier', 'com.bytedompteur.documentfinder.DocumentFinderMain', '--mac-app-category', 'public.app-category.productivity']
            osSpecificCryptomatorIntegrationsPackageRequired = 'org.cryptomator.integrations.mac'
        }
    }
    /*
     * All setting are found out in trial and error process. See also 'create-image-with-jlink.md' in the documentation
     * directory.
     */
    mergedModule {
        excludeProvides servicePattern: 'org.w3c.dom.DOMImplementationSourceList'
        excludeProvides servicePattern: 'org.xml.sax.driver'
        excludeProvides servicePattern: 'io.micrometer.context.ContextAccessor'
        excludeProvides servicePattern: 'reactor.blockhound.integration.BlockHoundIntegration'
//        requires 'jdk.jdwp.agent' // Required for remote debugging
        if (!operatingSystem.isLinux()) {
            requires osSpecificCryptomatorIntegrationsPackageRequired // OS specific tray and dock integration
        }
    }
    addExtraDependencies('integrations', 'javafx', 'annotations', 'jcl-over-slf4j')
    forceMerge("jna")
    forceMerge("log4j-api")
}


tasks.register('createVersionInfoPropertiesFile', WriteProperties) {
    description "Creates a version properties file in the build resources directory"
    dependsOn 'processResources'
    setDestinationFile(Paths.get(sourceSets.main.output.resourcesDir.getPath(), 'version-info.properties').toFile())
    property 'version', version
    property 'commit', project.property("git.commit")
    property 'commit-date', project.property("git.commit.timestamp.datetime")
}
processResources.configure {finalizedBy('createVersionInfoPropertiesFile')} // enforce property file creation after processing resources

tasks.register('printVersionSW', Task) {
    description "Print version info to console"
    dependsOn 'processResources'
    // log version info to console
    doLast {
        println "Version: ${project.version}"
        println "Version: ${getProject().getVersion()}"
        println "Commit: ${project.property("git.commit")}"
        println "Commit date: ${project.property("git.commit.timestamp.datetime")}"
    }
}