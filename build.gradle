import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

import java.nio.file.Paths
import java.util.regex.Pattern

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
    id 'me.qoomon.git-versioning' version '6.4.2'
    id 'org.gradlex.extra-java-module-info' version '1.4.2'
}

ext {
    lombokVersion = '1.18.30'
    slf4jVersion = '2.0.7'
    jclOverSlf4jVersion = '2.0.9'
    logbackVersion = '1.4.11'
    janinoVersion = "3.1.9"
    luceneVersion = '9.8.0'
    mockitoVersion = '5.4.0'
    junitVersion = '5.10.0'
    assertjVersion = '3.24.2'
    reactorVersion = '3.5.9'
    guavaVersion = '32.1.2-jre'
    tikaVersion = '2.8.0'
    daggerVersion = '2.47'
    javaFxVersion = '21'
    fontAwesomeFxVersion = '2.2.0-9.1.2'
    testFxVersion = '4.0.17'
    gsonVersion = '2.10.1'
    jsonAssertVersion = '1.5.1'
    jSystemThemeDetectorVersion = '3.8'
    failsafeVersion = '3.3.2'
    defaultMainClass = 'com.bytedompteur.documentfinder.DocumentFinderMain'
}

group 'com.bytedompteur'
version '1.0-SNAPSHOT'
OperatingSystem operatingSystem = DefaultNativePlatform.currentOperatingSystem
gitVersioning.apply {
    refs {
        considerTagsOnBranches = true
        tag('v(?<version>.*)') {
            version = '${ref.version}'
        }
        branch('.+') {
            version = '1.0-${ref}-SNAPSHOT'
        }

    }

    // optional fallback configuration in case of no matching ref configuration
    rev {
        version = '${commit}'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    all*.exclude group: 'xml-apis'
    // Exclude cryptomator linux integration dependencies, which lead to duplicate module error while executing jpackage
    all*.exclude group: 'org.purejava' // org.purejava:kdewallet
    all*.exclude group: 'de.swiesend' // de.swiesend:secret-service
}

dependencies {
    //  ============================================
    //  Runtime
    //  ============================================
    implementation "dev.failsafe:failsafe:${failsafeVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "org.slf4j:jcl-over-slf4j:${jclOverSlf4jVersion}" // Route Apache Tika logs to Logback
    implementation "org.codehaus.janino:janino:${janinoVersion}"
    implementation "org.apache.lucene:lucene-core:${luceneVersion}"
    implementation "org.apache.lucene:lucene-queryparser:${luceneVersion}"
    implementation "org.apache.lucene:lucene-analysis-common:${luceneVersion}"
    implementation "io.projectreactor:reactor-core:${reactorVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.apache.tika:tika-core:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-pdf-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-miscoffice-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-microsoft-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-apple-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-text-module:${tikaVersion}"
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "de.jensd:fontawesomefx-materialicons:${fontAwesomeFxVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.github.Dansoftowner:jSystemThemeDetector:${jSystemThemeDetectorVersion}"
    implementation 'io.github.g00fy2:versioncompare:1.4.1'

    //  ===================================================================================
    //  Operating system dependent runtime
    //  Because the application release build must be done on the same OS as the target OS
    //  ===================================================================================
    if (operatingSystem.isMacOsX()) {
        implementation "org.cryptomator:integrations-mac:1.2.2"
    } else if (operatingSystem.isWindows()) {
        implementation "org.cryptomator:integrations-win:1.2.3"
    } else if (operatingSystem.isLinux()) {
        implementation "org.cryptomator:integrations-linux:1.3.0"
    }

    //  ============================================
    //  Compile only
    //  ============================================
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    //  ============================================
    //  Test
    //  ============================================
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "com.google.dagger:dagger:${daggerVersion}"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation 'org.hamcrest:hamcrest:2.2' // Required for FXTest. The FXRobot has a direct dependency to hamcrest.

    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "io.projectreactor:reactor-test:${reactorVersion}"
    testImplementation "org.testfx:testfx-core:${testFxVersion}"
    testImplementation "org.testfx:testfx-junit5:${testFxVersion}"
    testImplementation "org.skyscreamer:jsonassert:${jsonAssertVersion}"
}

test {
    useJUnitPlatform()
    configurations {
        testRuntimeClasspath {
            attributes { attribute(Attribute.of("javaModule", Boolean), false) }
        }
    }
}

compileTestJava {
    dependsOn('createVersionInfoPropertiesFile')
    configurations {
        testCompileClasspath {
            attributes { attribute(Attribute.of("javaModule", Boolean), false) }
        }
    }
}

// https://github.com/gradlex-org/extra-java-module-info
extraJavaModuleInfo {
    module("io.github.g00fy2:versioncompare", "versioncompare") {
        exports("io.github.g00fy2.versioncompare")
    }

    automaticModule("xerces:xercesImpl", "xerces")
    automaticModule("org.codehaus.janino:janino", "janino")
    automaticModule("org.codehaus.janino:commons-compiler", "janino.compiler")
    automaticModule("com.google.guava:failureaccess", "guava.failureaccess")
    automaticModule("com.google.guava:listenablefuture", "guava.listenablefuture")
    automaticModule("com.google.code.findbugs:jsr305", "google.findbugs.jsr")
    automaticModule("com.google.j2objc:j2objc-annotations", "google.j2objc.annotations")
    automaticModule("org.apache.tika:tika-parser-mail-commons", "tika.parser.mail.commons")
    automaticModule("org.apache.james:apache-mime4j-dom", "apache.mime4j.dom")
    automaticModule("org.apache.james:apache-mime4j-core", "apache.mime4j.core")
    automaticModule("org.apache.pdfbox:jempbox", "pdfbox.jempbox")
    automaticModule("com.pff:java-libpst", "pff.java.libpst")
    automaticModule("commons-logging:commons-logging", "apache.commons.logging")
    automaticModule("com.googlecode.plist:dd-plist", "plist.dd")
    automaticModule("org.apache.commons:commons-math3", "apache.commons.math")
    automaticModule("org.apache.commons:commons-collections4", "org.apache.commons.collections")
    automaticModule("com.zaxxer:SparseBitSet", "zaxxer.sparsebitset")
    automaticModule("org.ccil.cowan.tagsoup:tagsoup", "tagsoup")
    automaticModule("com.google.dagger:dagger-compiler", "dagger.compiler")
    automaticModule("com.google.dagger:dagger-spi", "dagger.spi")
    automaticModule("com.google.dagger:dagger-producers", "dagger.producers")
    automaticModule("com.google.googlejavaformat:google-java-format", "google.googlejavaformat")
    automaticModule("com.google.devtools.ksp:symbol-processing-api", "google.devtools.ksp.symbol.processing.api")
    automaticModule("net.ltgt.gradle.incap:incap", "net.ltgt.gradle.incap")
    automaticModule("com.google.errorprone:javac-shaded", "google.errorprone.javac.shaded")
    automaticModule("org.jetbrains.kotlin:kotlin-stdlib-common", "kotlin.stdlib.common")
    automaticModule("org.jetbrains:annotations", "org.jetbrains.annotations")
    automaticModule("javax.inject:javax.inject", "javax.inject")
}

jar {
    dependsOn('createVersionInfoPropertiesFile')
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
        // Subdirectory in the project which acts as the application home dir while developing.
        excludeDirs += file("index-dev")
    }
}

//As documented here https://openjfx.io/openjfx-docs/#gradle
javafx {
    version = "${javaFxVersion}"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing', 'javafx.web' ]
}

application {
    mainClass = project.hasProperty('applicationMainClassOverwrite') ? project.getProperty('applicationMainClassOverwrite') : project.defaultMainClass
    mainModule = 'DocumentFinder.main'
}

jlink {
    def osSpecificCryptomatorIntegrationsPackageRequired = null
    def semverPattern = Pattern.compile("\\d.*\\.\\d.*\\.\\d.*")
    def isSnapshotBuild = !semverPattern.matcher("${version}").matches()
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--verbose', '--ignore-signing-information'] // Ignoring signing information is required since usage of Java 21 an Apache Tika parser (org.bouncycastle:bcpkix-jdk18on) but the signature-related files of the signed modular JARs aren't copied to the runtime image.
    launcher {
        name = isSnapshotBuild ? "DocumentFinder-SNAPSHOT-${project.property("git.commit")}" : "DocumentFinder"
    }
    jpackage {
        if (isSnapshotBuild)
            appVersion = '1.0.0'
        else
            appVersion = "${version}".replaceAll('v', '')


        vendor = 'DocumentFinder'

        if (operatingSystem.isWindows()) {
            installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-menu-group', 'DocumentFinder', '--win-upgrade-uuid', '420e9eb8-5b23-4589-88bb-94c1b0b70b8b', '--win-shortcut', '--about-url', 'https://github.com/sascha-wedekind/document-finder']
            icon = 'src/main/resources/images/DocumentFinderIcon.ico'
            installerType = 'msi'
            osSpecificCryptomatorIntegrationsPackageRequired = 'org.cryptomator.integrations.win'
        } else if (operatingSystem.isLinux()) {
            icon = 'src/main/resources/images/DocumentFinderIcon_512.png'
            installerType = 'deb'
            installerOptions = ['--about-url', 'https://github.com/sascha-wedekind/document-finder']
        } else if (operatingSystem.isMacOsX()) {
            icon = 'src/main/resources/images/DocumentFinderIcon_512.icns'
            installerType = 'dmg'
            jvmArgs = ['-Dapple.awt.enableTemplateImages=true', '-Dapple.awt.UIElement=true', '-Dapple.laf.useScreenMenuBar=true', '-Dsun.java2d.metal=true']
            installerOptions = ['--about-url', 'https://github.com/sascha-wedekind/document-finder', '--mac-package-name', 'DocumentFinder', '--mac-package-identifier', 'com.bytedompteur.documentfinder.DocumentFinderMain', '--mac-app-category', 'public.app-category.productivity']
            osSpecificCryptomatorIntegrationsPackageRequired = 'org.cryptomator.integrations.mac'
        }
    }
    /*
     * All setting are found out in trial and error process. See also 'create-image-with-jlink.md' in the documentation
     * directory.
     */
    mergedModule {
        excludeProvides servicePattern: 'org.w3c.dom.DOMImplementationSourceList'
        excludeProvides servicePattern: 'org.xml.sax.driver'
        excludeProvides servicePattern: 'io.micrometer.context.ContextAccessor'
        excludeProvides servicePattern: 'reactor.blockhound.integration.BlockHoundIntegration'
        //requires 'org.slf4j' // apache.tika.core requires org.slf4j
//        requires 'jdk.jdwp.agent' // Required for remote debugging
        if (!operatingSystem.isLinux()) {
            requires osSpecificCryptomatorIntegrationsPackageRequired // OS specific tray and dock integration
        }
    }
    addExtraDependencies('javafx')
    addExtraDependencies('integrations')
}


tasks.register('createVersionInfoPropertiesFile', WriteProperties) {
    description "Creates a version properties file in the build resources directory"
    dependsOn 'processResources'
    outputFile Paths.get(sourceSets.main.output.resourcesDir.getPath(), 'version-info.properties').toFile()
    property 'version', version
    property 'commit', project.property("git.commit")
    property 'commit-date', project.property("git.commit.timestamp.datetime")
}
processResources.configure {finalizedBy('createVersionInfoPropertiesFile')} // enforce property file creation after processing resources

tasks.register('printVersionSW', Task) {
    description "Print version info to console"
    dependsOn 'processResources'
    // log version info to console
    doLast {
        println "Version: ${project.version}"
        println "Version: ${getProject().getVersion()}"
        println "Commit: ${project.property("git.commit")}"
        println "Commit date: ${project.property("git.commit.timestamp.datetime")}"
    }
}