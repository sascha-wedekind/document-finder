import java.nio.file.Paths
import java.util.regex.Pattern

//https://github.com/qoomon/gradle-git-versioning-plugin

plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id "org.javamodularity.moduleplugin" version "1.8.12"
    id 'me.qoomon.git-versioning' version '6.3.0'
}

ext {
    lombokVersion = '1.18.22'
    slf4jVersion = '2.0.2'
    jclOverSlf4jVersion = '1.7.36'
    logbackVersion = '1.2.11'
    janinoVersion = "3.1.6"
    luceneVersion = '9.3.0'
    junitVersion = '5.9.0'
    mockitoVersion = '4.7.0'
    assertjVersion = '3.23.1'
    reactorVersion = '3.4.22'
    guavaVersion = '31.1-jre'
    tikaVersion = '2.4.1'
    daggerVersion = '2.43.2'
    javaFxVersion = '18.0.2'
    fontAwesomeFxVersion = '2.2.0-9.1.2'
    testFxVersion = '4.0.16-alpha'
    gsonVersion = "2.9.1"
    jsonAssertVersion = '1.5.1'
    xercesImplVersion = '2.12.2'
    jSystemThemeDetectorVersion = '3.8'
    failsafeVersion = '3.2.4'
    defaultMainClass = 'com.bytedompteur.documentfinder.DocumentFinderMain'
}

group 'com.bytedompteur'
version '1.0-SNAPSHOT'
gitVersioning.apply {
    refs {
        considerTagsOnBranches = true
        tag('v(?<version>.*)') {
            version = '${ref.version}'
        }
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }

    }

    // optional fallback configuration in case of no matching ref configuration
    rev {
        version = '${commit}'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    all*.exclude group: 'xml-apis'
    all*.exclude group: 'commons-logging'
}

dependencies {
    //  ============================================
    //  Runtime
    //  ============================================
    implementation "xerces:xercesImpl:${xercesImplVersion}"
    implementation "dev.failsafe:failsafe:${failsafeVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "org.slf4j:jcl-over-slf4j:${jclOverSlf4jVersion}" // Route Apache Tika logs to Logback
    implementation "org.codehaus.janino:janino:${janinoVersion}"
    implementation "org.apache.lucene:lucene-core:${luceneVersion}"
    implementation "org.apache.lucene:lucene-queryparser:${luceneVersion}"
    implementation "org.apache.lucene:lucene-analysis-common:${luceneVersion}"
    implementation "io.projectreactor:reactor-core:${reactorVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.apache.tika:tika-core:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-pdf-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-miscoffice-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-microsoft-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-apple-module:${tikaVersion}"
    implementation "org.apache.tika:tika-parser-text-module:${tikaVersion}"
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "de.jensd:fontawesomefx-materialicons:${fontAwesomeFxVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.github.Dansoftowner:jSystemThemeDetector:${jSystemThemeDetectorVersion}"

    //  ============================================
    //  Compile only
    //  ============================================
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    //  ============================================
    //  Test
    //  ============================================
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "io.projectreactor:reactor-test:${reactorVersion}"
    testImplementation "org.testfx:testfx-core:${testFxVersion}"
    testImplementation "org.testfx:testfx-junit5:${testFxVersion}"
    testImplementation "org.skyscreamer:jsonassert:${jsonAssertVersion}"

}

test {
    useJUnitPlatform()
    // Disable Java modules (Jigsaw) while running tests. See gradle-modules-plugin.
    moduleOptions {
        runOnClasspath = true
    }
}

compileTestJava {
    // Disable Java modules (Jigsaw) while running tests. See gradle-modules-plugin.
    moduleOptions {
        compileOnClasspath = true
    }
}

compileJava {
    moduleOptions {
        version = project.property("git.commit.timestamp")
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
        // Subdirectory in the project which acts as the application home dir while developing.
        excludeDirs += file("index-dev")
    }
}

//As documented here https://openjfx.io/openjfx-docs/#gradle
javafx {
    version = "${javaFxVersion}"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing' ]
}

application {
    mainClass = project.hasProperty('applicationMainClassOverwrite') ? project.getProperty('applicationMainClassOverwrite') : project.defaultMainClass
    mainModule = 'DocumentFinder.main'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--verbose']
    launcher {
        name = "DocumentFinder-${version}"
    }
    jpackage {
        def semverPattern = Pattern.compile("v\\d.*\\.\\d.*\\.\\d.*")
        if (semverPattern.matcher("${version}").matches())
            appVersion = "${version}".replaceAll('v', '')
        else
            appVersion = '0.0.0'

        OperatingSystem os = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem;
        if (os.isWindows()) {
            installerOptions = ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            icon = 'src/main/resources/images/DocumentFinderIcon.ico'
        } else {
            installerOptions = []
        }
    }
    /*
     * All setting are found out in trial and error process. See also 'create-image-with-jlink.md' in the documentation
     * directory.
     */
    mergedModule {
        excludeProvides servicePattern: 'org.w3c.dom.DOMImplementationSourceList'
        excludeProvides servicePattern: 'org.xml.sax.driver'
        excludeProvides servicePattern: 'org.apache.xmlbeans.impl.store.PathDelegate.SelectPathInterface'
        excludeProvides servicePattern: 'javax.servlet.ServletContainerInitializer'
        excludeProvides servicePattern: 'org.apache.xmlbeans.impl.store.QueryDelegate.QueryInterface'
        excludeProvides servicePattern: 'reactor.blockhound.integration.BlockHoundIntegration'
    }
    addExtraDependencies('javafx')
}


tasks.register('createVersionInfoPropertiesFile', WriteProperties) {
    description "Creates a version properties file in the build resources directory"
    dependsOn 'processResources'
    outputFile Paths.get(sourceSets.main.output.resourcesDir.getPath(), 'version-info.properties').toFile()
    property 'version', version
    property 'commit', project.property("git.commit")
    property 'commit-date', project.property("git.commit.timestamp.datetime")
}
processResources.configure {finalizedBy('createVersionInfoPropertiesFile')} // enforce property file creation after processing resources
